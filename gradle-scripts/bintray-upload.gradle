/*
 * Copyright 2015 Two Toasters
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/*
 * Usage:
 *    (1) Add the classpath declarations to your project's root build.gradle's buildscript's dependencies section
 *
 *           classpath 'com.github.dcendents:android-maven-plugin:1.2'
 *           classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
 *
 *    (2) Setup the necessary global, project, and module gradle properties
 *
 *           # Global properties - those in your gradle home's gradle.properties file (usually in ~/.gradle/gradle.properties)
 *           BINTRAY_REPO=maven
 *           BINTRAY_ORGANIZATION=your_org_name
 *           BINTRAY_USER=your_bintray_username
 *           BINTRAY_API_KEY=your_api_key
 *           BINTRAY_GPG_PASSWORD=your_private_gpg_key_passphrase
 *           BINTRAY_OSS_USER=your_sonatype_username
 *           BINTRAY_OSS_PASSWORD=your_sonatype_password
 *
 *           # Project properties - those in your project's gradle.properties file (usually in the root dir of your project repo)
 *           POM_DESCRIPTION=A short description of your Android library
 *           POM_URL=https://github.com/twotoasters/servos
 *           POM_SCM_URL=https://github.com/twotoasters/servos
 *           POM_SCM_CONNECTION=scm:git@github.com:twotoasters/servos.git
 *           POM_SCM_DEV_CONNECTION=scm:git@github.com:twotoasters/servos.git
 *           POM_LICENCE_NAME=The Apache Software License, Version 2.0
 *           POM_LICENCE_URL=http://www.apache.org/licenses/LICENSE-2.0.txt
 *           POM_LICENCE_DIST=repo
 *           POM_DEVELOPER_ID=your_github_id
 *           POM_DEVELOPER_NAME=your_developer_real_name
 *           BINTRAY_PROJECT_NAME=com.twotoasters.servos
 *           BINTRAY_PROJECT_LICENSE=Apache-2.0
 *           BINTRAY_SITE_URL=https://github.com/twotoasters/servos
 *           BINTRAY_GIT_URL=https://github.com/twotoasters/servos.git
 *
 *           # Module properties - those in your module's gradle.properties file (usually in the your module's dir)
 *           POM_NAME=servos
 *           POM_ARTIFACT_ID=util
 *           POM_PACKAGING=pick_jar_or_aar
 *
 *    (3) Run: ./gradlew clean build bintrayUpload
 */

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

install {
    repositories.mavenInstaller {
        pom.project {
            name POM_NAME
            packaging POM_PACKAGING
            description POM_DESCRIPTION
            url POM_URL

            scm {
                url POM_SCM_URL
                connection POM_SCM_CONNECTION
                developerConnection POM_SCM_DEV_CONNECTION
            }

            licenses {
                license {
                    name POM_LICENCE_NAME
                    url POM_LICENCE_URL
                    distribution POM_LICENCE_DIST
                }
            }

            developers {
                developer {
                    id POM_DEVELOPER_ID
                    name POM_DEVELOPER_NAME
                }
            }
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

bintray {
    user = BINTRAY_USER
    key = BINTRAY_API_KEY

    configurations = ['archives']
    dryRun = false
    publish = true
    pkg {
        repo = BINTRAY_REPO
        userOrg = BINTRAY_ORGANIZATION
        name = BINTRAY_PROJECT_NAME
        websiteUrl = BINTRAY_SITE_URL
        vcsUrl = BINTRAY_GIT_URL
        licenses = [BINTRAY_PROJECT_LICENSE]
        version {
            gpg {
                sign = true // Determines whether to GPG sign the files. The default is false
                passphrase = BINTRAY_GPG_PASSWORD // Optional. The passphrase for GPG signing'
            }
            mavenCentralSync {
                sync = true // Optional (true by default). Determines whether to sync the version to Maven Central.
                user = BINTRAY_OSS_USER // OSS user token
                password = BINTRAY_OSS_PASSWORD // OSS user password
                close = '1' // Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}
